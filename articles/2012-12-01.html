Title: Introduction to OX
Topic: OX
Author: Jesse Luehrs <jesse.luehrs@iinteractive.com>

=head1 Overview

L<OX> is a new web framework, designed to allow you to easily glue together
existing components (or write new, highly decoupled components) for your
application, without requiring additional plugin systems, glue layers, or
global state. It is available on L<CPAN|OX>, and on
L<GitHub|http://github.com/iinteractive/OX>. You can install it via C<cpanm
OX>. If you have any questions or suggestions, we hang out on the C<#ox>
channel on irc.perl.org, so feel free to drop by!

OX is based on three main components:

=head2 Plack

L<Plack> is an implementation of the L<PSGI> protocol. By implementing PSGI, OX
can run on any server platform that PSGI supports (which is most of them).
Plack also provides extra functionality such as middleware, which OX exposes to
application authors (as will be described in future articles).

=head2 Bread::Board

L<Bread::Board> is a dependency injection framework, used to structure your
application initialization. It allows your application objects to access each
other without the need for globals or repetitive argument passing. With
Bread::Board, you can write your application classes however makes sense to
you, and then tie them all together.

=head2 Path::Router

L<Path::Router> provides simple, reversible routing for URL paths. In addition
to basic path matching, it supports variable path components (with optional
validation), the ability to look up paths based on the descriptions they were
defined with, and useful debugging and testing tools. It's used to determine
your application's URL structure.

=head1 Hello World

=begin vim perl

  use OX;

  router as {
      route '/' => sub { "Hello world!" };
  };

=end vim

This defines a simple "Hello world" application. This snippet is a valid
C<.psgi> file (the format used by L<Plack>), since the C<router> keyword
returns a PSGI application coderef if necessary.

=for html
If you download this example <a href="hello.psgi">here</a>, you can run it with
<tt>plackup hello.psgi</tt>, and view the results in your browser.

The router block is the most important part of an OX application. It defines
the entire URL structure in one place, making tracing the code that is run for
a given request as easy as possible to find. Simple routes can define the code
to be run directly, but for anything non-trivial, you'll want to organize the
code into models, controllers, etc., as shown in this next example.

=head1 Another example

=begin vim perl

  package Counter::Model;
  use Moose;

  has counter => (
      traits  => ['Counter'],
      is      => 'ro',
      isa     => 'Int',
      default => 0,
      handles => {
          inc   => 'inc',
          dec   => 'dec',
          reset => 'reset',
      },
  );

  package Counter::Controller;
  use Moose;

  has model => (
      is       => 'ro',
      isa      => 'Counter::Model',
      required => 1,
  );

  sub index {
      my $self = shift;
      return $self->model->counter;
  }

  sub inc {
      my $self = shift;
      return $self->model->inc;
  }

  sub dec {
      my $self = shift;
      return $self->model->dec;
  }

  sub reset {
      my $self = shift;
      return $self->model->reset;
  }

  package Counter;
  use OX;

  has model => (
      is        => 'ro',
      isa       => 'Counter::Model',
      lifecycle => 'Singleton',
  );

  has controller => (
      is    => 'ro',
      isa   => 'Counter::Controller',
      infer => 1,
  );

  router as {
      route '/'      => 'controller.index';
      route '/inc'   => 'controller.inc';
      route '/dec'   => 'controller.dec';
      route '/reset' => 'controller.reset';
  };

=end vim

This is a slightly more involved example. Here, paths are routed to methods on
controller objects, and OX automatically figures out (via Bread::Board) how to
create all of the objects necessary to make the request work.

=for html
This example can be downloaded <a href="counter.psgi">here</a>, and run via
<tt>plackup counter.psgi</tt>. Access the <tt>/inc</tt>, <tt>/dec</tt>, and
<tt>/reset</tt> paths to manipulate the counter.

One interesting thing to note is how the controller and model are both just
normal Moose classes - there is nothing special about them at all. Your
application can be structured in any way that you want, and OX will do the work
of introspecting and tying together all of the pieces. We will explain how
exactly this works over the coming weeks.

=cut
